/**
 * Template API Module
 * Public interfaces for the Template ecosystem.
 */
plugins {
    id 'java-library'
    id 'maven-publish'
}

dependencies {

    compileOnly 'io.papermc.paper:paper-api:1.21.11-R0.1-SNAPSHOT'
    
    // API needs the core Adventure library for the 'Component' class in interfaces
    compileOnly 'net.kyori:adventure-api:4.26.1'
}

// Define variables for cleaner URL construction
def gitOwner = project.property('git-org-name')
def gitRepo = project.property('git-org-repository')
def gitUrl = project.property('template-api-artifact-url')

publishing {
    publications {
        create('mavenJava', MavenPublication) {
            artifactId = project.property('template-api-artifact-id')
            artifact shadowJar

            pom {
                name = project.property('template-api-name')
                description = project.property('template-api-artifact-description')
                url = gitUrl

                developers {
                    developer {
                        id = 'jetsadawijit'
                        url = 'https://jetsadawijit.github.io'
                    }
                }

                scm {
                    connection = "scm:git:git://github.com/${gitOwner}/${gitRepo}.git"
                    developerConnection = "scm:git:ssh://github.com/${gitOwner}/${gitRepo}.git"
                    url = gitUrl
                }
            }
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/${gitOwner}/${gitRepo}")

            credentials {
                username = System.getenv("USER_GITHUB_NAME")
                password = System.getenv("USER_GITHUB_TOKEN")
            }
        }
    }
}
